version 1.0

## Copyright Broad Institute, 2017
##
## This WDL workflow runs GATK4 Mutect 2 on a single tumor-normal pair or on a single tumor sample,
## and performs additional filtering and functional annotation tasks.
##
## Main requirements/expectations :
## - One analysis-ready BAM file (and its index) for each sample
##
## Description of inputs:
##
## ** Runtime **
## gatk_docker: docker image to use for GATK 4 Mutect2
## preemptible: how many preemptions to tolerate before switching to a non-preemptible machine (on Google)
## max_retries: how many times to retry failed tasks -- very important on the cloud when there are transient errors
## gatk_override: (optional) local file or Google bucket path to a GATK 4 java jar file to be used instead of the GATK 4 jar
##                in the docker image.  This must be supplied when running in an environment that does not support docker
##                (e.g. SGE cluster on a Broad on-prem VM)
##
## ** Workflow options **
## intervals: genomic intervals (will be used for scatter)
## scatter_count: number of parallel jobs to generate when scattering over intervals
## m2_extra_args, m2_extra_filtering_args: additional arguments for Mutect2 calling and filtering (optional)
## split_intervals_extra_args: additional arguments for splitting intervals before scattering (optional)
## run_orientation_bias_mixture_model_filter: (optional) if true, filter orientation bias sites with the read orientation artifact mixture model.
##
## ** Primary inputs **
## ref_fasta, ref_fai, ref_dict: reference genome, index, and dictionary
## tumor_bam, tumor_bam_index: BAM and index for the tumor sample
## normal_bam, normal_bam_index: BAM and index for the normal sample
##
## ** Primary resources ** (optional but strongly recommended)
## pon, pon_idx: optional panel of normals (and its index) in VCF format containing probable technical artifacts (false positves)
## gnomad, gnomad_idx: optional database of known germline variants (and its index) (see http://gnomad.broadinstitute.org/downloads)
## variants_for_contamination, variants_for_contamination_idx: VCF of common variants (and its index)with allele frequencies for calculating contamination
##
## ** Secondary resources ** (for optional tasks)
## realignment_index_bundle: resource for FilterAlignmentArtifacts, which runs if and only if it is specified.  Generated by BwaMemIndexImageCreator.
##
## Funcotator parameters (see Funcotator help for more details).
## funco_reference_version: "hg19" for hg19 or b37.  "hg38" for hg38.  Default: "hg19"
## funco_output_format: "MAF" to produce a MAF file, "VCF" to procude a VCF file.  Default: "MAF"
## funco_compress: (Only valid if funco_output_format == "VCF" )  If true, will compress the output of Funcotator.  If false, produces an uncompressed output file.  Default: false
## funco_use_gnomad_AF: If true, will include gnomAD allele frequency annotations in output by connecting to the internet to query gnomAD (this impacts performance).  If false, will not annotate with gnomAD.  Default: false
## funco_transcript_selection_mode: How to select transcripts in Funcotator.  ALL, CANONICAL, or BEST_EFFECT
## funco_transcript_selection_list: Transcripts (one GENCODE ID per line) to give priority during selection process.
## funco_data_sources_tar_gz:  Funcotator datasources tar gz file.  Bucket location is recommended when running on the cloud.
## funco_annotation_defaults:  Default values for annotations, when values are unspecified.  Specified as  <ANNOTATION>:<VALUE>.  For example:  "Center:Broad"
## funco_annotation_overrides:  Values for annotations, even when values are unspecified.  Specified as  <ANNOTATION>:<VALUE>.  For example:  "Center:Broad"
## funcotator_excluded_fields:  Annotations that should not appear in the output (VCF or MAF).  Specified as  <ANNOTATION>.  For example:  "ClinVar_ALLELEID"
## funco_filter_funcotations: If true, will only annotate variants that have passed filtering (. or PASS value in the FILTER column).  If false, will annotate all variants in the input file.  Default: true
## funcotator_extra_args: Any additional arguments to pass to Funcotator.  Default: ""
##
## Outputs :
## - One VCF file and its index with primary filtering applied; secondary filtering and functional annotation if requested; a bamout.bam
##   file of reassembled reads if requested
##
## Cromwell version support
## - Successfully tested on v34
##
## LICENSING :
## This script is released under the WDL source code license (BSD-3) (see LICENSE in
## https://github.com/broadinstitute/wdl). Note however that the programs it calls may
## be subject to different licenses. Users are responsible for checking that they are
## authorized to run all programs before running this script. Please see the docker
## pages at https://hub.docker.com/r/broadinstitute/* for detailed licensing information
## pertaining to the included programs.

struct Runtime {
    String gatk_docker
    File? gatk_override
    Int max_retries
    Int preemptible
    Int cpu
    Int machine_mem
    Int command_mem
    Int disk
    Int boot_disk_size
}

workflow Mutect2 {
    input {
      # Mutect2 inputs
      File? intervals
      File ref_fasta
      File ref_fai
      File ref_dict
      File tumor_reads
      File tumor_reads_index
      File? normal_reads
      File? normal_reads_index
      File? pon
      File? pon_idx
      Int scatter_count
      File? gnomad
      File? gnomad_idx
      File? variants_for_contamination
      File? variants_for_contamination_idx
      File? realignment_index_bundle
      String? realignment_extra_args
      Boolean? run_orientation_bias_mixture_model_filter
      String? m2_extra_args
      String? m2_extra_filtering_args
      String? getpileupsummaries_extra_args
      String? split_intervals_extra_args
      Boolean? make_bamout
      Boolean? compress_vcfs
      File? gga_vcf
      File? gga_vcf_idx


      # mosaic
      Float ? low_af
      Float ? high_af
      Boolean? pcr_free = true
      File segdup_bed #SegDup_and_clustered.GRCh38.bed
      File allrepeats_bed #allrepeats_forindel.hg38.bed
      File umap_file
      File feature_extraction_script
      File avail_acess_1000g
      String cov
      File prediction_script
      File model_snv
      File model_ins
      File model_del
      Int? emergency_extra_disk
      File humandb_refGene
      File humandb_refGeneMrna
      File humandb_dbnsfp42a
      File gnomad_2_1_1_0_001
      File gnomad_2_1_1_0

      # Funcotator inputs
      Boolean? run_funcotator
      String? sequencing_center
      String? sequence_source
      String? funco_reference_version
      String? funco_output_format
      Boolean? funco_compress
      Boolean? funco_use_gnomad_AF
      File? funco_data_sources_tar_gz
      String? funco_transcript_selection_mode
      File? funco_transcript_selection_list
      Array[String]? funco_annotation_defaults
      Array[String]? funco_annotation_overrides
      Array[String]? funcotator_excluded_fields
      Boolean? funco_filter_funcotations
      String? funcotator_extra_args

      String funco_default_output_format = "MAF"

      # runtime
      String gatk_docker
      File? gatk_override
      String basic_bash_docker = "ubuntu:16.04"
      Boolean? filter_funcotations

      Int? preemptible
      Int? max_retries
      Int small_task_cpu = 2
      Int small_task_mem = 4
      Int small_task_disk = 100
      Int boot_disk_size = 12
      Int learn_read_orientation_mem = 8000
      Int filter_alignment_artifacts_mem = 9000

      # Use as a last resort to increase the disk given to every task in case of ill behaving data
      Int? emergency_extra_disk

      # These are multipliers to multipler inputs by to make sure we have enough disk to accommodate for possible output sizes
      # Large is for Bams/WGS vcfs
      # Small is for metrics/other vcfs
      Float large_input_to_output_multiplier = 2.25
      Float small_input_to_output_multiplier = 2.0
      Float cram_to_bam_multiplier = 6.0
    }

    Int preemptible_or_default = select_first([preemptible, 2])
    Int max_retries_or_default = select_first([max_retries, 2])

    Boolean compress = select_first([compress_vcfs, false])
    Boolean run_ob_filter = select_first([run_orientation_bias_mixture_model_filter, false])
    Boolean make_bamout_or_default = select_first([make_bamout, false])
    Boolean run_funcotator_or_default = select_first([run_funcotator, false])
    Boolean filter_funcotations_or_default = select_first([filter_funcotations, true])

    # Disk sizes used for dynamic sizing
    Int ref_size = ceil(size(ref_fasta, "GB") + size(ref_dict, "GB") + size(ref_fai, "GB"))
    Int tumor_reads_size = ceil(size(tumor_reads, "GB") + size(tumor_reads_index, "GB"))
    Int gnomad_vcf_size = if defined(gnomad) then ceil(size(gnomad, "GB")) else 0
    Int normal_reads_size = if defined(normal_reads) then ceil(size(normal_reads, "GB") + size(normal_reads_index, "GB")) else 0

    # If no tar is provided, the task downloads one from broads ftp server
    Int funco_tar_size = if defined(funco_data_sources_tar_gz) then ceil(size(funco_data_sources_tar_gz, "GB") * 3) else 100
    Int gatk_override_size = if defined(gatk_override) then ceil(size(gatk_override, "GB")) else 0

    # This is added to every task as padding, should increase if systematically you need more disk for every call
    Int disk_pad = 10 + gatk_override_size + select_first([emergency_extra_disk,0])

    # logic about output file names -- these are the names *without* .vcf extensions
    String output_basename = basename(basename(tumor_reads, ".bam"),".cram")  #hacky way to strip either .bam or .cram
    String unfiltered_name = output_basename + "-unfiltered"
    String filtered_name = output_basename + "-filtered"
    String funcotated_name = output_basename + "-funcotated"

    String output_vcf_name = output_basename + ".vcf"

    Int tumor_cram_to_bam_disk = ceil(tumor_reads_size * cram_to_bam_multiplier)
    Int normal_cram_to_bam_disk = ceil(normal_reads_size * cram_to_bam_multiplier)

    Runtime standard_runtime = {"gatk_docker": gatk_docker, "gatk_override": gatk_override,
            "max_retries": max_retries_or_default, "preemptible": preemptible_or_default, "cpu": small_task_cpu,
            "machine_mem": small_task_mem * 1000, "command_mem": small_task_mem * 1000 - 500,
            "disk": small_task_disk + disk_pad, "boot_disk_size": boot_disk_size}

    if (basename(tumor_reads) != basename(tumor_reads, ".cram")) {
        call CramToBam as TumorCramToBam {
            input:
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                cram = tumor_reads,
                crai = tumor_reads_index,
                name = output_basename,
                disk_size = tumor_cram_to_bam_disk
        }
    }

    String normal_or_empty = select_first([normal_reads, ""])
    if (basename(normal_or_empty) != basename(normal_or_empty, ".cram")) {
        String normal_basename = basename(basename(normal_or_empty, ".bam"),".cram")
        call CramToBam as NormalCramToBam {
            input:
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                cram = normal_reads,
                crai = normal_reads_index,
                name = normal_basename,
                disk_size = normal_cram_to_bam_disk
        }
    }

    File tumor_bam = select_first([TumorCramToBam.output_bam, tumor_reads])
    File tumor_bai = select_first([TumorCramToBam.output_bai, tumor_reads_index])
    File? normal_bam = if defined(normal_reads) then select_first([NormalCramToBam.output_bam, normal_reads]) else normal_reads
    File? normal_bai = if defined(normal_reads) then select_first([NormalCramToBam.output_bai, normal_reads_index]) else normal_reads_index

    Int tumor_bam_size = ceil(size(tumor_bam, "GB") + size(tumor_bai, "GB"))
    Int normal_bam_size = if defined(normal_bam) then ceil(size(normal_bam, "GB") + size(normal_bai, "GB")) else 0

    Int m2_output_size = tumor_bam_size / scatter_count
    #TODO: do we need to change this disk size now that NIO is always going to happen (for the google backend only)
    Int m2_per_scatter_size = (tumor_bam_size + normal_bam_size) + ref_size + gnomad_vcf_size + m2_output_size + disk_pad

    call SplitIntervals {
        input:
            intervals = intervals,
            ref_fasta = ref_fasta,
            ref_fai = ref_fai,
            ref_dict = ref_dict,
            scatter_count = scatter_count,
            split_intervals_extra_args = split_intervals_extra_args,
            runtime_params = standard_runtime
    }

    scatter (subintervals in SplitIntervals.interval_files ) {
        call M2 {
            input:
                intervals = subintervals,
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                tumor_bam = tumor_bam,
                tumor_bai = tumor_bai,
                normal_bam = normal_bam,
                normal_bai = normal_bai,
                pon = pon,
                pon_idx = pon_idx,
                gnomad = gnomad,
                gnomad_idx = gnomad_idx,
                preemptible = preemptible,
                max_retries = max_retries,
                m2_extra_args = m2_extra_args,
                getpileupsummaries_extra_args = getpileupsummaries_extra_args,
                variants_for_contamination = variants_for_contamination,
                variants_for_contamination_idx = variants_for_contamination_idx,
                make_bamout = make_bamout_or_default,
                run_ob_filter = run_ob_filter,
                compress = compress,
                gga_vcf = gga_vcf,
                gga_vcf_idx = gga_vcf_idx,
                gatk_override = gatk_override,
                gatk_docker = gatk_docker,
                disk_space = m2_per_scatter_size
        }
    }

    Int merged_vcf_size = ceil(size(M2.unfiltered_vcf, "GB"))
    Int merged_bamout_size = ceil(size(M2.output_bamOut, "GB"))

    if (run_ob_filter) {
        call LearnReadOrientationModel {
            input:
                f1r2_tar_gz = M2.f1r2_counts,
                runtime_params = standard_runtime,
                mem = learn_read_orientation_mem
        }
    }

    call MergeVCFs {
        input:
            input_vcfs = M2.unfiltered_vcf,
            input_vcf_indices = M2.unfiltered_vcf_idx,
            output_name = unfiltered_name,
            compress = compress,
            runtime_params = standard_runtime
    }

    if (make_bamout_or_default) {
        call MergeBamOuts {
            input:
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                bam_outs = M2.output_bamOut,
                output_vcf_name = basename(MergeVCFs.merged_vcf, ".vcf"),
                runtime_params = standard_runtime,
                disk_space = ceil(merged_bamout_size * large_input_to_output_multiplier) + disk_pad,
        }
    }

    call MergeStats { input: stats = M2.stats, runtime_params = standard_runtime }

    if (defined(variants_for_contamination)) {
        call MergePileupSummaries as MergeTumorPileups {
            input:
                input_tables = flatten(M2.tumor_pileups),
                output_name = output_basename,
                ref_dict = ref_dict,
                runtime_params = standard_runtime
        }

        if (defined(normal_bam)){
            call MergePileupSummaries as MergeNormalPileups {
                input:
                    input_tables = flatten(M2.normal_pileups),
                    output_name = output_basename,
                    ref_dict = ref_dict,
                    runtime_params = standard_runtime
            }
        }

        call CalculateContamination {
            input:
                tumor_pileups = MergeTumorPileups.merged_table,
                normal_pileups = MergeNormalPileups.merged_table,
                runtime_params = standard_runtime
        }
    }

    call Filter {
        input:
            ref_fasta = ref_fasta,
            ref_fai = ref_fai,
            ref_dict = ref_dict,
            intervals = intervals,
            unfiltered_vcf = MergeVCFs.merged_vcf,
            unfiltered_vcf_idx = MergeVCFs.merged_vcf_idx,
            output_name = filtered_name,
            compress = compress,
            mutect_stats = MergeStats.merged_stats,
            contamination_table = CalculateContamination.contamination_table,
            maf_segments = CalculateContamination.maf_segments,
            artifact_priors_tar_gz = LearnReadOrientationModel.artifact_prior_table,
            m2_extra_filtering_args = m2_extra_filtering_args,
            runtime_params = standard_runtime,
            disk_space = ceil(size(MergeVCFs.merged_vcf, "GB") * small_input_to_output_multiplier) + disk_pad
    }

    if (defined(realignment_index_bundle)) {
        call FilterAlignmentArtifacts {
            input:
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                bam = tumor_bam,
                bai = tumor_bai,
                realignment_index_bundle = select_first([realignment_index_bundle]),
                realignment_extra_args = realignment_extra_args,
                compress = compress,
                output_name = filtered_name,
                input_vcf = Filter.filtered_vcf,
                input_vcf_idx = Filter.filtered_vcf_idx,
                runtime_params = standard_runtime,
                mem = filter_alignment_artifacts_mem
        }
    }

    if (run_funcotator_or_default) {
        File funcotate_vcf_input = select_first([FilterAlignmentArtifacts.filtered_vcf, Filter.filtered_vcf])
        File funcotate_vcf_input_index = select_first([FilterAlignmentArtifacts.filtered_vcf_idx, Filter.filtered_vcf_idx])
        call Funcotate {
            input:
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                input_vcf = funcotate_vcf_input,
                input_vcf_idx = funcotate_vcf_input_index,
                reference_version = select_first([funco_reference_version, "hg19"]),
                output_file_base_name = basename(funcotate_vcf_input, ".vcf") + ".annotated",
                output_format = if defined(funco_output_format) then "" + funco_output_format else funco_default_output_format,
                compress = if defined(funco_compress) then select_first([funco_compress]) else false,
                use_gnomad = if defined(funco_use_gnomad_AF) then select_first([funco_use_gnomad_AF]) else false,
                data_sources_tar_gz = funco_data_sources_tar_gz,
                case_id = M2.tumor_sample[0],
                control_id = M2.normal_sample[0],
                sequencing_center = sequencing_center,
                sequence_source = sequence_source,
                transcript_selection_mode = funco_transcript_selection_mode,
                transcript_selection_list = funco_transcript_selection_list,
                annotation_defaults = funco_annotation_defaults,
                annotation_overrides = funco_annotation_overrides,
                funcotator_excluded_fields = funcotator_excluded_fields,
                filter_funcotations = filter_funcotations_or_default,
                extra_args = funcotator_extra_args,
                runtime_params = standard_runtime,
                disk_space = ceil(size(funcotate_vcf_input, "GB") * large_input_to_output_multiplier)  + funco_tar_size + disk_pad
        }
    }

    # Conditionally set low_af based on whether pcr_free is true
    Float default_low_af = select_first([low_af,0.02])
    Float final_low_af = if (defined(pcr_free) && pcr_free) then 0.03 else default_low_af
    Float final_high_af = select_first([high_af, 0.4])
    call MT2InitialFilter {
        input:
            input_vcf = Filter.filtered_vcf,
            low_af = final_low_af,
            high_af = final_high_af
    }

    call repeat_filter {
        input:
            mt2pon_bed = MT2InitialFilter.mt2_initial_filter_bed,
            segdup_bed = segdup_bed
    }

    call annovar_formatter {
        input:
            input_bed = repeat_filter.filter_segdup_bed
    }

    call MAF0_extraction_SNV {
        input:
            input_list = annovar_formatter.output_variant
    }

    call MAF0_extraction_INS {
        input:
            input_list = annovar_formatter.output_variant,
            allrepeats_bed = allrepeats_bed
    }

    call MAF0_extraction_DEL {
        input:
            input_list = annovar_formatter.output_variant,
            allrepeats_bed = allrepeats_bed
    }

    call FeatureExtraction {
        input:
            bed_file_snv = MAF0_extraction_SNV.output_bed_snv,
            bed_file_ins = MAF0_extraction_INS.output_bed_ins,
            bed_file_del = MAF0_extraction_DEL.output_bed_del,
            umap_file = umap_file,
            bam_file = tumor_bam,
            bam_index = tumor_bai,
            reference_fasta = ref_fasta,
            feature_extraction_script = feature_extraction_script
    }

    call Filter1000G {
        input:
            feature_snv = FeatureExtraction.output_feature_snv,
            feature_ins = FeatureExtraction.output_feature_ins,
            feature_del = FeatureExtraction.output_feature_del,
            avail_acess_1000g = avail_acess_1000g
    }

    call PredictionMosaic {
        input:
            cov=cov,
            prediction_script = prediction_script,
            model_snv = model_snv,
            model_del = model_del,
            model_ins = model_ins,
            features_snv = Filter1000G.output_filtered_snv,
            features_del = Filter1000G.output_filtered_del,
            features_ins = Filter1000G.output_filtered_ins
    }

    # Set multipliers and padding

    call AnnotationMosaic {
        input:
            predictions_snv = PredictionMosaic.predictions_snv,
            predictions_ins = PredictionMosaic.predictions_ins,
            predictions_del = PredictionMosaic.predictions_del,
            humandb_refGene = humandb_refGene,
            humandb_refGeneMrna = humandb_refGeneMrna,
            humandb_dbnsfp42a = humandb_dbnsfp42a
    }

    call ExtractSubvcf {
        input:
            vcf_gz = Filter.filtered_vcf,
            predictions_snv = AnnotationMosaic.input_predictions_snv,
            predictions_ins = AnnotationMosaic.input_predictions_ins,
            predictions_del = AnnotationMosaic.input_predictions_del
    }

    call gnomAD_filter_vcf_anno {
        input:
            vcf = ExtractSubvcf.sub_vcf_snv,
            anno = AnnotationMosaic.output_predictions_snv,
            gnomad_2_1_1_0_001 = gnomad_2_1_1_0_001,
            gnomad_2_1_1_0 = gnomad_2_1_1_0
    }

    scatter(subintervals in SplitIntervals.interval_files) {
        call DepthOfCoverage {
            input:
                intervals = subintervals,
                ref_fasta = ref_fasta,
                ref_fai = ref_fai,
                ref_dict = ref_dict,
                tumor_bam = tumor_bam,
                tumor_bam_index = tumor_bai,
                gatk_docker = gatk_docker,
                disk_space = m2_per_scatter_size
        }
    }

    call MergeCovStatistics {
        input:
            statistics = DepthOfCoverage.depth_of_cov_statistics
    }

    output {
        File filtered_vcf = select_first([FilterAlignmentArtifacts.filtered_vcf, Filter.filtered_vcf])
        File filtered_vcf_idx = select_first([FilterAlignmentArtifacts.filtered_vcf_idx, Filter.filtered_vcf_idx])
        File filtering_stats = Filter.filtering_stats
        File mutect_stats = MergeStats.merged_stats
        File? contamination_table = CalculateContamination.contamination_table

        File? funcotated_file = Funcotate.funcotated_output_file
        File? funcotated_file_index = Funcotate.funcotated_output_file_index
        File? bamout = MergeBamOuts.merged_bam_out
        File? bamout_index = MergeBamOuts.merged_bam_out_index
        File? maf_segments = CalculateContamination.maf_segments
        File? read_orientation_model_params = LearnReadOrientationModel.artifact_prior_table

        File mt2_initial_filter_bed = MT2InitialFilter.mt2_initial_filter_bed
        File mt2_segdup_bed = repeat_filter.filter_segdup_bed
        File var_anno= annovar_formatter.output_variant
        File extraction_snv = MAF0_extraction_SNV.output_bed_snv
        File extraction_ins = MAF0_extraction_INS.output_bed_ins
        File extraction_del = MAF0_extraction_DEL.output_bed_del
        File feature_snv = FeatureExtraction.output_feature_snv
        File feature_ins = FeatureExtraction.output_feature_ins
        File feature_del = FeatureExtraction.output_feature_del
        File filter_1000g_snv = Filter1000G.output_filtered_snv
        File filter_1000g_ins = Filter1000G.output_filtered_ins
        File filter_1000g_del = Filter1000G.output_filtered_del
        File mosaic_snv = PredictionMosaic.predictions_snv
        File mosaic_ins = PredictionMosaic.predictions_ins
        File mosaic_del = PredictionMosaic.predictions_del
        File in_anno_snv = AnnotationMosaic.input_predictions_snv
        File in_anno_ins = AnnotationMosaic.input_predictions_ins
        File in_anno_del = AnnotationMosaic.input_predictions_del
        File anno_snv = AnnotationMosaic.output_predictions_snv
        File anno_ins = AnnotationMosaic.output_predictions_ins
        File anno_del = AnnotationMosaic.output_predictions_del
        File snv_sub_vcf = ExtractSubvcf.sub_vcf_snv
        File snv_subvcf_bed = ExtractSubvcf.subvcf_bed_snv
        File snv_geno_dp_af = ExtractSubvcf.geno_dp_af_snv
        File ins_sub_vcf = ExtractSubvcf.sub_vcf_ins
        File ins_subvcf_bed = ExtractSubvcf.subvcf_bed_ins
        File ins_geno_dp_af = ExtractSubvcf.geno_dp_af_ins
        File del_sub_vcf = ExtractSubvcf.sub_vcf_del
        File del_subvcf_bed = ExtractSubvcf.subvcf_bed_del
        File del_geno_dp_af = ExtractSubvcf.geno_dp_af_del
        File vcf3 = gnomAD_filter_vcf_anno.vcf3
        File vcf4 = gnomAD_filter_vcf_anno.vcf4
        File anno3 = gnomAD_filter_vcf_anno.anno3
        File anno4 = gnomAD_filter_vcf_anno.anno4
        File final_tier = gnomAD_filter_vcf_anno.tier
        File stat_all = MergeCovStatistics.stat_all
    }
}

task CramToBam {
    input {
      File ref_fasta
      File ref_fai
      File ref_dict
      #cram and crai must be optional since Normal cram is optional
      File? cram
      File? crai
      String name
      Int disk_size
      Int? mem
    }

    Int machine_mem = if defined(mem) then mem * 1000 else 6000

    #Calls samtools view to do the conversion
    command {
        #Set -e and -o says if any command I run fails in this script, make sure to return a failure
        set -e
        set -o pipefail

        samtools view -h -T ~{ref_fasta} ~{cram} |
            samtools view -b -o ~{name}.bam -
        samtools index -b ~{name}.bam
        mv ~{name}.bam.bai ~{name}.bai
    }

    runtime {
        docker: "us.gcr.io/broad-gotc-prod/genomes-in-the-cloud:2.3.3-1513176735"
        memory: machine_mem + " MB"
        disks: "local-disk " + disk_size + " HDD"
    }

    output {
        File output_bam = "~{name}.bam"
        File output_bai = "~{name}.bai"
    }
}

task SplitIntervals {
    input {
      File? intervals
      File ref_fasta
      File ref_fai
      File ref_dict
      Int scatter_count
      String? split_intervals_extra_args

      # runtime
      Runtime runtime_params
    }

    command {
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

        mkdir interval-files
        gatk --java-options "-Xmx~{runtime_params.command_mem}m" SplitIntervals \
            -R ~{ref_fasta} \
            ~{"-L " + intervals} \
            -scatter ~{scatter_count} \
            -O interval-files \
            ~{split_intervals_extra_args}
        cp interval-files/*.interval_list .
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        Array[File] interval_files = glob("*.interval_list")
    }
}

task M2 {
    input {
      File? intervals
      File ref_fasta
      File ref_fai
      File ref_dict
      File tumor_bam
      File tumor_bai
      File? normal_bam
      File? normal_bai
      File? pon
      File? pon_idx
      File? gnomad
      File? gnomad_idx
      String? m2_extra_args
      String? getpileupsummaries_extra_args
      Boolean? make_bamout
      Boolean? run_ob_filter
      Boolean compress
      File? gga_vcf
      File? gga_vcf_idx
      File? variants_for_contamination
      File? variants_for_contamination_idx

      File? gatk_override

      # runtime
      String gatk_docker
      Int? mem
      Int? preemptible
      Int? max_retries
      Int? disk_space
      Int? cpu
      Boolean use_ssd = false
    }

    String output_vcf = "output" + if compress then ".vcf.gz" else ".vcf"
    String output_vcf_idx = output_vcf + if compress then ".tbi" else ".idx"

    String output_stats = output_vcf + ".stats"

    # Mem is in units of GB but our command and memory runtime values are in MB
    Int machine_mem = if defined(mem) then mem * 1000 else 3500
    Int command_mem = machine_mem - 500

    parameter_meta{
      intervals: {localization_optional: true}
      ref_fasta: {localization_optional: true}
      ref_fai: {localization_optional: true}
      ref_dict: {localization_optional: true}
      tumor_bam: {localization_optional: true}
      tumor_bai: {localization_optional: true}
      normal_bam: {localization_optional: true}
      normal_bai: {localization_optional: true}
      pon: {localization_optional: true}
      pon_idx: {localization_optional: true}
      gnomad: {localization_optional: true}
      gnomad_idx: {localization_optional: true}
      gga_vcf: {localization_optional: true}
      gga_vcf_idx: {localization_optional: true}
      variants_for_contamination: {localization_optional: true}
      variants_for_contamination_idx: {localization_optional: true}
    }

    command <<<
        set -e

        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" gatk_override}

        # We need to create these files regardless, even if they stay empty
        touch bamout.bam
        touch f1r2.tar.gz
        echo "" > normal_name.txt

        gatk --java-options "-Xmx~{command_mem}m" GetSampleName -R ~{ref_fasta} -I ~{tumor_bam} -O tumor_name.txt -encode
        tumor_command_line="-I ~{tumor_bam} -tumor `cat tumor_name.txt`"

        if [[ ! -z "~{normal_bam}" ]]; then
            gatk --java-options "-Xmx~{command_mem}m" GetSampleName -R ~{ref_fasta} -I ~{normal_bam} -O normal_name.txt -encode
            normal_command_line="-I ~{normal_bam} -normal `cat normal_name.txt`"
        fi

        gatk --java-options "-Xmx~{command_mem}m" Mutect2 \
            -R ~{ref_fasta} \
            $tumor_command_line \
            $normal_command_line \
            ~{"--germline-resource " + gnomad} \
            ~{"-pon " + pon} \
            ~{"-L " + intervals} \
            ~{"--alleles " + gga_vcf} \
            -O "~{output_vcf}" \
            ~{true='--bam-output bamout.bam' false='' make_bamout} \
            ~{true='--f1r2-tar-gz f1r2.tar.gz' false='' run_ob_filter} \
            ~{m2_extra_args}

        m2_exit_code=$?

        ### GetPileupSummaries

        # If the variants for contamination and the intervals for this scatter don't intersect, GetPileupSummaries
        # throws an error.  However, there is nothing wrong with an empty intersection for our purposes; it simply doesn't
        # contribute to the merged pileup summaries that we create downstream.  We implement this by with array outputs.
        # If the tool errors, no table is created and the glob yields an empty array.
        set +e

        if [[ ! -z "~{variants_for_contamination}" ]]; then
            gatk --java-options "-Xmx~{command_mem}m" GetPileupSummaries -R ~{ref_fasta} -I ~{tumor_bam} ~{"--interval-set-rule INTERSECTION -L " + intervals} \
                -V ~{variants_for_contamination} -L ~{variants_for_contamination} -O tumor-pileups.table ~{getpileupsummaries_extra_args}


            if [[ ! -z "~{normal_bam}" ]]; then
                gatk --java-options "-Xmx~{command_mem}m" GetPileupSummaries -R ~{ref_fasta} -I ~{normal_bam} ~{"--interval-set-rule INTERSECTION -L " + intervals} \
                    -V ~{variants_for_contamination} -L ~{variants_for_contamination} -O normal-pileups.table ~{getpileupsummaries_extra_args}
            fi
        fi

        # the script only fails if Mutect2 itself fails
        exit $m2_exit_code
    >>>

    runtime {
        docker: gatk_docker
        bootDiskSizeGb: 12
        memory: machine_mem + " MB"
        disks: "local-disk " + select_first([disk_space, 100]) + if use_ssd then " SSD" else " HDD"
        preemptible: select_first([preemptible, 10])
        maxRetries: select_first([max_retries, 0])
        cpu: select_first([cpu, 1])
    }

    output {
        File unfiltered_vcf = "~{output_vcf}"
        File unfiltered_vcf_idx = "~{output_vcf_idx}"
        File output_bamOut = "bamout.bam"
        String tumor_sample = read_string("tumor_name.txt")
        String normal_sample = read_string("normal_name.txt")
        File stats = "~{output_stats}"
        File f1r2_counts = "f1r2.tar.gz"
        Array[File] tumor_pileups = glob("*tumor-pileups.table")
        Array[File] normal_pileups = glob("*normal-pileups.table")
    }
}

task MergeVCFs {
    input {
      Array[File] input_vcfs
      Array[File] input_vcf_indices
      String output_name
      Boolean compress
      Runtime runtime_params
    }

    String output_vcf = output_name + if compress then ".vcf.gz" else ".vcf"
    String output_vcf_idx = output_vcf + if compress then ".tbi" else ".idx"

    # using MergeVcfs instead of GatherVcfs so we can create indices
    # WARNING 2015-10-28 15:01:48 GatherVcfs  Index creation not currently supported when gathering block compressed VCFs.
    command {
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}
        gatk --java-options "-Xmx~{runtime_params.command_mem}m" MergeVcfs -I ~{sep=' -I ' input_vcfs} -O ~{output_vcf}
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File merged_vcf = "~{output_vcf}"
        File merged_vcf_idx = "~{output_vcf_idx}"
    }
}

task MergeBamOuts {
    input {
      File ref_fasta
      File ref_fai
      File ref_dict
      Array[File]+ bam_outs
      String output_vcf_name
      Runtime runtime_params
      Int? disk_space   #override to request more disk than default small task params
    }

    command <<<
        # This command block assumes that there is at least one file in bam_outs.
        #  Do not call this task if len(bam_outs) == 0
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}
        gatk --java-options "-Xmx~{runtime_params.command_mem}m" GatherBamFiles \
            -I ~{sep=" -I " bam_outs} -O unsorted.out.bam -R ~{ref_fasta}

        # We must sort because adjacent scatters may have overlapping (padded) assembly regions, hence
        # overlapping bamouts

        gatk --java-options "-Xmx~{runtime_params.command_mem}m" SortSam -I unsorted.out.bam \
            -O ~{output_vcf_name}.out.bam \
            --SORT_ORDER coordinate -VALIDATION_STRINGENCY LENIENT
        gatk --java-options "-Xmx~{runtime_params.command_mem}m" BuildBamIndex -I ~{output_vcf_name}.out.bam -VALIDATION_STRINGENCY LENIENT
    >>>

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + select_first([disk_space, runtime_params.disk]) + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File merged_bam_out = "~{output_vcf_name}.out.bam"
        File merged_bam_out_index = "~{output_vcf_name}.out.bai"
    }
}


task MergeStats {
    input {
      Array[File]+ stats
      Runtime runtime_params
    }

    command {
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}


        gatk --java-options "-Xmx~{runtime_params.command_mem}m" MergeMutectStats \
            -stats ~{sep=" -stats " stats} -O merged.stats
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File merged_stats = "merged.stats"
    }
}

task MergePileupSummaries {
    input {
      Array[File] input_tables
      String output_name
      File ref_dict
      Runtime runtime_params
    }

    command {
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

        gatk --java-options "-Xmx~{runtime_params.command_mem}m" GatherPileupSummaries \
        --sequence-dictionary ~{ref_dict} \
        -I ~{sep=' -I ' input_tables} \
        -O ~{output_name}.tsv
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File merged_table = "~{output_name}.tsv"
    }
}

# Learning step of the orientation bias mixture model, which is the recommended orientation bias filter as of September 2018
task LearnReadOrientationModel {
    input {
      Array[File] f1r2_tar_gz
      Runtime runtime_params
      Int? mem  #override memory
    }

    Int machine_mem = select_first([mem, runtime_params.machine_mem])
    Int command_mem = machine_mem - 1000

    command {
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

        gatk --java-options "-Xmx~{command_mem}m" LearnReadOrientationModel \
            -I ~{sep=" -I " f1r2_tar_gz} \
            -O "artifact-priors.tar.gz"
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File artifact_prior_table = "artifact-priors.tar.gz"
    }

}

task CalculateContamination {
    input {
      String? intervals
      File tumor_pileups
      File? normal_pileups
      Runtime runtime_params
    }

    command {
        set -e

        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

        gatk --java-options "-Xmx~{runtime_params.command_mem}m" CalculateContamination -I ~{tumor_pileups} \
        -O contamination.table --tumor-segmentation segments.table ~{"-matched " + normal_pileups}
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File contamination_table = "contamination.table"
        File maf_segments = "segments.table"
    }
}

task Filter {
    input {
      File? intervals
      File ref_fasta
      File ref_fai
      File ref_dict
      File unfiltered_vcf
      File unfiltered_vcf_idx
      String output_name
      Boolean compress
      File? mutect_stats
      File? artifact_priors_tar_gz
      File? contamination_table
      File? maf_segments
      String? m2_extra_filtering_args

      Runtime runtime_params
      Int? disk_space
    }

    String output_vcf = output_name + if compress then ".vcf.gz" else ".vcf"
    String output_vcf_idx = output_vcf + if compress then ".tbi" else ".idx"

    parameter_meta{
      ref_fasta: {localization_optional: true}
      ref_fai: {localization_optional: true}
      ref_dict: {localization_optional: true}
    }

    command {
        set -e

        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

        gatk --java-options "-Xmx~{runtime_params.command_mem}m" FilterMutectCalls -V ~{unfiltered_vcf} \
            -R ~{ref_fasta} \
            -O ~{output_vcf} \
            ~{"--contamination-table " + contamination_table} \
            ~{"--tumor-segmentation " + maf_segments} \
            ~{"--ob-priors " + artifact_priors_tar_gz} \
            ~{"-stats " + mutect_stats} \
            --filtering-stats filtering.stats \
            ~{m2_extra_filtering_args}
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + select_first([disk_space, runtime_params.disk]) + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File filtered_vcf = "~{output_vcf}"
        File filtered_vcf_idx = "~{output_vcf_idx}"
        File filtering_stats = "filtering.stats"
    }
}

task FilterAlignmentArtifacts {
    input {
      File ref_fasta
      File ref_fai
      File ref_dict
      File input_vcf
      File input_vcf_idx
      File bam
      File bai
      String output_name
      Boolean compress
      File realignment_index_bundle
      String? realignment_extra_args
      Runtime runtime_params
      Int mem
    }

    String output_vcf = output_name + if compress then ".vcf.gz" else ".vcf"
    String output_vcf_idx = output_vcf +  if compress then ".tbi" else ".idx"

    Int machine_mem = mem
    Int command_mem = machine_mem - 500

    parameter_meta{
      ref_fasta: {localization_optional: true}
      ref_fai: {localization_optional: true}
      ref_dict: {localization_optional: true}
      input_vcf: {localization_optional: true}
      input_vcf_idx: {localization_optional: true}
      bam: {localization_optional: true}
      bai: {localization_optional: true}
    }

    command {
        set -e

        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

        gatk --java-options "-Xmx~{command_mem}m" FilterAlignmentArtifacts \
            -R ~{ref_fasta} \
            -V ~{input_vcf} \
            -I ~{bam} \
            --bwa-mem-index-image ~{realignment_index_bundle} \
            ~{realignment_extra_args} \
            -O ~{output_vcf}
    }

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: machine_mem + " MB"
        disks: "local-disk " + runtime_params.disk + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

    output {
        File filtered_vcf = "~{output_vcf}"
        File filtered_vcf_idx = "~{output_vcf_idx}"
    }
}

task Funcotate {
     input {
       File ref_fasta
       File ref_fai
       File ref_dict
       File input_vcf
       File input_vcf_idx
       String reference_version
       String output_file_base_name
       String output_format
       Boolean compress
       Boolean use_gnomad
       # This should be updated when a new version of the data sources is released
       # TODO: Make this dynamically chosen in the command.
       File? data_sources_tar_gz = "gs://broad-public-datasets/funcotator/funcotator_dataSources.v1.6.20190124s.tar.gz"
       String? control_id
       String? case_id
       String? sequencing_center
       String? sequence_source
       String? transcript_selection_mode
       File? transcript_selection_list
       Array[String]? annotation_defaults
       Array[String]? annotation_overrides
       Array[String]? funcotator_excluded_fields
       Boolean? filter_funcotations
       File? interval_list

       String? extra_args

       # ==============
       Runtime runtime_params
       Int? disk_space   #override to request more disk than default small task params

       # You may have to change the following two parameter values depending on the task requirements
       Int default_ram_mb = 3000
       # WARNING: In the workflow, you should calculate the disk space as an input to this task (disk_space_gb).  Please see [TODO: Link from Jose] for examples.
       Int default_disk_space_gb = 100
     }

     # ==============
     # Process input args:
     String output_maf = output_file_base_name + ".maf"
     String output_maf_index = output_maf + ".idx"
     String output_vcf = output_file_base_name + if compress then ".vcf.gz" else ".vcf"
     String output_vcf_idx = output_vcf +  if compress then ".tbi" else ".idx"
     String output_file = if output_format == "MAF" then output_maf else output_vcf
     String output_file_index = if output_format == "MAF" then output_maf_index else output_vcf_idx
     String transcript_selection_arg = if defined(transcript_selection_list) then " --transcript-list " else ""
     String annotation_def_arg = if defined(annotation_defaults) then " --annotation-default " else ""
     String annotation_over_arg = if defined(annotation_overrides) then " --annotation-override " else ""
     String filter_funcotations_args = if defined(filter_funcotations) && (filter_funcotations) then " --remove-filtered-variants " else ""
     String excluded_fields_args = if defined(funcotator_excluded_fields) then " --exclude-field " else ""
     String interval_list_arg = if defined(interval_list) then " -L " else ""
     String extra_args_arg = select_first([extra_args, ""])

     String dollar = "$"

     parameter_meta{
      ref_fasta: {localization_optional: true}
      ref_fai: {localization_optional: true}
      ref_dict: {localization_optional: true}
      input_vcf: {localization_optional: true}
      input_vcf_idx: {localization_optional: true}
     }

     command <<<
         set -e
         export GATK_LOCAL_JAR=~{default="/root/gatk.jar" runtime_params.gatk_override}

         # Extract our data sources:
         echo "Extracting data sources zip file..."
         mkdir datasources_dir
         tar zxvf ~{data_sources_tar_gz} -C datasources_dir --strip-components 1
         DATA_SOURCES_FOLDER="$PWD/datasources_dir"

         # Handle gnomAD:
         if ~{use_gnomad} ; then
             echo "Enabling gnomAD..."
             for potential_gnomad_gz in gnomAD_exome.tar.gz gnomAD_genome.tar.gz ; do
                 if [[ -f ~{dollar}{DATA_SOURCES_FOLDER}/~{dollar}{potential_gnomad_gz} ]] ; then
                     cd ~{dollar}{DATA_SOURCES_FOLDER}
                     tar -zvxf ~{dollar}{potential_gnomad_gz}
                     cd -
                 else
                     echo "ERROR: Cannot find gnomAD folder: ~{dollar}{potential_gnomad_gz}" 1>&2
                     false
                 fi
             done
         fi

         # Run Funcotator:
         gatk --java-options "-Xmx~{runtime_params.command_mem}m" Funcotator \
             --data-sources-path $DATA_SOURCES_FOLDER \
             --ref-version ~{reference_version} \
             --output-file-format ~{output_format} \
             -R ~{ref_fasta} \
             -V ~{input_vcf} \
             -O ~{output_file} \
             ~{interval_list_arg} ~{default="" interval_list} \
             --annotation-default normal_barcode:~{default="Unknown" control_id} \
             --annotation-default tumor_barcode:~{default="Unknown" case_id} \
             --annotation-default Center:~{default="Unknown" sequencing_center} \
             --annotation-default source:~{default="Unknown" sequence_source} \
             ~{"--transcript-selection-mode " + transcript_selection_mode} \
             ~{transcript_selection_arg}~{default="" sep=" --transcript-list " transcript_selection_list} \
             ~{annotation_def_arg}~{default="" sep=" --annotation-default " annotation_defaults} \
             ~{annotation_over_arg}~{default="" sep=" --annotation-override " annotation_overrides} \
             ~{excluded_fields_args}~{default="" sep=" --exclude-field " funcotator_excluded_fields} \
             ~{filter_funcotations_args} \
             ~{extra_args_arg}
         # Make sure we have a placeholder index for MAF files so this workflow doesn't fail:
         if [[ "~{output_format}" == "MAF" ]] ; then
            touch ~{output_maf_index}
         fi
     >>>

    runtime {
        docker: runtime_params.gatk_docker
        bootDiskSizeGb: runtime_params.boot_disk_size
        memory: runtime_params.machine_mem + " MB"
        disks: "local-disk " + select_first([disk_space, runtime_params.disk]) + " HDD"
        preemptible: runtime_params.preemptible
        maxRetries: runtime_params.max_retries
        cpu: runtime_params.cpu
    }

     output {
         File funcotated_output_file = "~{output_file}"
         File funcotated_output_file_index = "~{output_file_index}"
     }
}

task MT2InitialFilter {
    input {
        File input_vcf
        Float low_af 
        Float high_af
    }
    String sample = basename(input_vcf, "-filtered.vcf")
    String output_bed = sample + ".init.bed"

    command <<<
        set -e

        echo "Processing input VCF file..."
        echo ~{input_vcf}
        echo ~{low_af}
        echo ~{high_af}

        input_vcf=~{input_vcf}
        if [[ $input_vcf == *.gz ]]; then
            cat_cmd="zcat"
        else
            cat_cmd="cat"
        fi

        echo "Running filtering command..."

        $cat_cmd $input_vcf | grep -v "^#" | grep PASS | \
        gawk '{match($0,/;POPAF=(([0-9]+\.[0-9]+));/,arr); if(arr[1]!~/-/ && arr[1]>=4){print $0}}' | \
        cut -f1,2,4,5,10 | sed "s/:/\t/g" | sed "s/,/\t/g" | \
        awk '$8>= ~{low_af} && $8< ~{high_af}' | grep -v '0|1' | grep -v '1|0' > temp1.bed

        $cat_cmd $input_vcf | grep -v "^#" | grep PASS | \
        gawk '{match($0,/;POPAF=(([0-9]+\.[0-9]+));/,arr); if(arr[1]!~/-/ && arr[1]>=4){print $0}}' | \
        cut -f1,2,4,5,10 | sed "s/:/\t/g" | sed "s/,/\t/g" | \
        awk '$8>= 0.02 && $8< ~{high_af}' | grep -E "0\|1|1\|0" > temp2.bed

        cat temp1.bed temp2.bed | \
        cut -f 1-4,6-8 | awk -v sample="~{sample}" '{OFS="\t";print $1,$2-1,$2,$3,$4,sample,$5,$6,$7}' \
        > ~{output_bed}

        echo "Checking output file..."
        if [ ! -s ~{output_bed} ]; then
            echo "Error: Output file is empty. Check the previous commands for errors." >&2
            exit 1
        fi

        echo "Filtering complete. Output file: ~{output_bed}"
        set +e
    >>>

    output {
        File mt2_initial_filter_bed = "~{output_bed}"
    }

    runtime {
        docker: "junhuili/terra_init:latest"
        cpu: 2
        memory: "8 GB"
        disks: "local-disk 50 SSD"
    }
}

task repeat_filter {
    input {
        File mt2pon_bed
        File segdup_bed
    }

    # Extract sample name from the input file name
    String output_filename = sub(basename(mt2pon_bed), "init\\.bed$", "noSegDup\\.bed")

    command {
        subtractBed -a ${mt2pon_bed} -b ${segdup_bed} > ${output_filename}
    }

    output {
        File filter_segdup_bed = "${output_filename}"
    }

    runtime {
        docker: "quay.io/biocontainers/bedtools:2.30.0--h7d7f7ad_2"
    }
}

task annovar_formatter {
    input {
        File input_bed
    }
    String output_bed = sub(basename(input_bed),"noSegDup\\.bed$","anno\\.list")

    command <<<
        cat ~{input_bed} | awk '{OFS="\t";len=length($4)-length($5);if(len<=0){print $1,$3,$3,$4,$5,$6}if(len>0){print $1,$3,$3+len,$4,$5,$6}}' > ~{output_bed}
        sed -i 's/chr//g' ~{output_bed}
    >>>

    output {
        File output_variant = "~{output_bed}"
    }

    runtime {
        docker: "ubuntu:latest"
    }
}

task MAF0_extraction_SNV {
    input {
        File input_list
    }
    String out_bed = sub(basename(input_list),"anno\\.list", "MAF0\\.SNV\\.chr\\.bed")

    command <<<
        cat ~{input_list} | awk '{OFS="\t";print $1,$2-1,$2,$4,$5,$6}' | awk 'length($4)==1 && length($5)==1' | awk 'BEGIN{OFS="\t"} $1="chr"$1' > ~{out_bed}
    >>>

    output {
        File output_bed_snv = "~{out_bed}"
    }

    runtime {
        docker: "ubuntu:latest"
    }
}

task MAF0_extraction_INS {
    input {
        File input_list
        File allrepeats_bed
    }
    String out_bed = sub(basename(input_list),"anno\\.list", "MAF0\\.INS\\.chr\\.bed")

    command <<<
        cat ~{input_list} | awk '{OFS="\t";print $1,$2-1,$2,$4,$5,$6}' | awk 'length($4) < length($5)' > temp_ins.bed
        bedtools subtract -a temp_ins.bed -b ~{allrepeats_bed} | awk 'BEGIN{OFS="\t"} $1="chr"$1' > ~{out_bed}
    >>>

    output {
        File output_bed_ins = "~{out_bed}"
    }

    runtime {
        docker: "quay.io/biocontainers/bedtools:2.30.0--h7d7f7ad_2"
    }
}

task MAF0_extraction_DEL {
    input {
        File input_list
        File allrepeats_bed
    }
    String out_bed = sub(basename(input_list),"anno\\.list", "MAF0\\.DEL\\.chr\\.bed")

    command <<<
        cat ~{input_list} | awk '{OFS="\t";print $1,$2-1,$2,$4,$5,$6}' | awk 'length($4) > length($5)' > temp_del.bed
        bedtools subtract -a temp_del.bed -b ~{allrepeats_bed} | awk 'BEGIN{OFS="\t"} $1="chr"$1' > ~{out_bed}
    >>>

    output {
        File output_bed_del = "~{out_bed}"
    }

    runtime {
        docker: "quay.io/biocontainers/bedtools:2.30.0--h7d7f7ad_2"
    }
}

task FeatureExtraction {
    input {
        File bed_file_snv
        File bed_file_ins
        File bed_file_del
        File umap_file
        File bam_file
        File bam_index
        File reference_fasta
        File feature_extraction_script
    }
    String sample_name=sub(basename(bed_file_snv),"\\.MAF0\\.SNV\\.chr\\.bed","")
    command <<<
        set -e
        export PATH=/opt/conda/bin:$PATH
        source activate py3.7.1 || conda activate py3.7.1
        export PYTHONWARNINGS="ignore"
        
        # Create a directory for BAM files
        bam_dir=$(dirname ~{bam_file})
        
        python ~{feature_extraction_script} \
            ~{bed_file_snv} \
            ~{sample_name}.SNV.features \
            $bam_dir \
            ~{reference_fasta} \
            ~{umap_file} \
            1 bam > ~{sample_name}.SNV.features.log 2>&1 &

        python ~{feature_extraction_script} \
            ~{bed_file_ins} \
            ~{sample_name}.INS.features \
            $bam_dir \
            ~{reference_fasta} \
            ~{umap_file} \
            1 bam > ~{sample_name}.INS.features.log 2>&1 &

        python ~{feature_extraction_script} \
            ~{bed_file_del} \
            ~{sample_name}.DEL.features \
            $bam_dir \
            ~{reference_fasta} \
            ~{umap_file} \
            1 bam > ~{sample_name}.DEL.features.log 2>&1 &
        wait
        
        conda deactivate
        set +e
    >>>

    output {
        File output_feature_snv = "~{sample_name}.SNV.features"
        File output_log_snv = "~{sample_name}.SNV.features.log"
        File output_feature_ins = "~{sample_name}.INS.features"
        File output_log_ins = "~{sample_name}.INS.features.log"
        File output_feature_del = "~{sample_name}.DEL.features"
        File output_log_del = "~{sample_name}.DEL.features.log"
    }

    runtime {
        docker: "junhuili/terra_py_tools:latest"
        cpu: 4
        memory: "32 GB"
        disks: "local-disk 1000 SSD"
        preemptible: 3
    }
}

task Filter1000G {
    input {
        File feature_snv
        File feature_ins
        File feature_del
        File avail_acess_1000g
    }
    String sample_name = sub(basename(feature_snv),"\\.SNV\\.features","")
    command <<<
        cat <(head -n 1 ~{feature_snv}) <(awk 'NR==FNR{c[$1,$2]=$0}NR!=FNR{if(c[$1,$2]){print $0}}' \
            <(bedtools intersect -a <(sed 's/~/\t/g' ~{feature_snv} | awk '{print $2"\t"$3"\t"$3}' | grep -v "conflict_num") \
            -b ~{avail_acess_1000g} -wa) \
            <(paste <(sed 's/~/\t/g' ~{feature_snv} | cut -f 2-3) ~{feature_snv}) | cut -f 3-) > ~{sample_name}.SNV.no1000g.features &

        cat <(head -n 1 ~{feature_ins}) <(awk 'NR==FNR{c[$1,$2]=$0}NR!=FNF{if(c[$1,$2]){print $0}}' \
            <(bedtools intersect -a <(sed 's/~/\t/g' ~{feature_ins} | awk '{print $2"\t"$3"\t"$3}' | grep -v "conflict_num") \
            -b ~{avail_acess_1000g} -wa) \
            <(paste <(sed 's/~/\t/g' ~{feature_ins} | cut -f 2-3) ~{feature_ins}) | cut -f 3-) > ~{sample_name}.INS.no1000g.features &

        cat <(head -n 1 ~{feature_del}) <(awk 'NR==FNR{c[$1,$2]=$0}NR!=FNF{if(c[$1,$2]){print $0}}' \
            <(bedtools intersect -a <(sed 's/~/\t/g' ~{feature_del} | awk '{print $2"\t"$3"\t"$3}' | grep -v "conflict_num") \
            -b ~{avail_acess_1000g} -wa) \
            <(paste <(sed 's/~/\t/g' ~{feature_del} | cut -f 2-3) ~{feature_del}) | cut -f 3-) > ~{sample_name}.DEL.no1000g.features &
        wait
    >>>

    output {
        File output_filtered_snv= "~{sample_name}.SNV.no1000g.features"
        File output_filtered_ins= "~{sample_name}.INS.no1000g.features"
        File output_filtered_del= "~{sample_name}.DEL.no1000g.features"
    }

    runtime {
        docker: "junhuili/terra_py_tools:latest"
        cpu: 2
        memory: "8 GB"
        disks: "local-disk 100 SSD"
    }
}

task PredictionMosaic {
    input {
        String cov="50X"
        File prediction_script
        File model_snv
        File model_ins
        File model_del
        File features_snv
        File features_del
        File features_ins
    }
    String sample_name = sub(basename(features_snv),"\\.SNV\\.no1000g\\.features","")

    command {
        Rscript ${prediction_script} \
        ${features_snv} \
        ${model_snv} \
        Refine \
        ${sample_name}.${cov}.SNV.predictions &

        Rscript ${prediction_script} \
        ${features_ins} \
        ${model_ins} \
        Refine \
        ${sample_name}.${cov}.INS.predictions &

        Rscript ${prediction_script} \
        ${features_del} \
        ${model_del} \
        Refine \
        ${sample_name}.${cov}.DEL.predictions &
        wait
    }

    output {
        File predictions_snv = "${sample_name}.${cov}.SNV.predictions"
        File predictions_ins = "${sample_name}.${cov}.INS.predictions"
        File predictions_del = "${sample_name}.${cov}.DEL.predictions"
    }

    runtime {
        docker: "junhuili/terra_r:latest"
    }
}

task AnnotationMosaic {
    input {
        File predictions_snv
        File predictions_ins
        File predictions_del
        File humandb_refGene
        File humandb_refGeneMrna
        File humandb_dbnsfp42a
    }

    String sample_name=sub(basename(predictions_snv),"\\.SNV\\.predictions","")
    command <<<
        set -e
        humandb=$(dirname ~{humandb_refGene})
        grep "mosaic" ~{predictions_snv} | cut -f 1,35 | awk 'BEGIN { FS = "~" } ; { print $2"\t"$3"\t"$3"\t"$4"\t"$5"\t"$6 }' | tail -n +2 | awk '$6=="mosaic" {print $0}' > ~{sample_name}.SNV.input.predictions.txt
        cat <(grep "hap=3" ~{predictions_ins}) | awk 'BEGIN { FS = "~" } ; { print $2"\t"$3"\t"$3"\t"$4"\t"$5 }' | tail -n +2 > ~{sample_name}.INS.input.predictions.txt
        cat <(grep "hap=3" ~{predictions_del}) | awk 'BEGIN { FS = "~" } ; { print $2"\t"$3"\t"$3+length($4)-1"\t"$4"\t"$5 }' | tail -n +2 > ~{sample_name}.DEL.input.predictions.txt
    
        # Run ANNOVAR
        perl /usr/src/app/table_annovar.pl ~{sample_name}.SNV.input.predictions.txt $humandb -buildver hg38 -out ~{sample_name}.SNV.output.predictions -remove -protocol refGene,dbnsfp42a -operation g,f -nastring .
        perl /usr/src/app/table_annovar.pl ~{sample_name}.INS.input.predictions.txt $humandb -buildver hg38 -out ~{sample_name}.INS.output.predictions -remove -protocol refGene,dbnsfp42a -operation g,f -nastring .
        perl /usr/src/app/table_annovar.pl ~{sample_name}.DEL.input.predictions.txt $humandb -buildver hg38 -out ~{sample_name}.DEL.output.predictions -remove -protocol refGene,dbnsfp42a -operation g,f -nastring .
        echo "after anno"
        set +e
    >>>

    output {
        File input_predictions_snv = "${sample_name}.SNV.input.predictions.txt"
        File output_predictions_snv = "${sample_name}.SNV.output.predictions.hg38_multianno.txt"

        File input_predictions_ins = "${sample_name}.INS.input.predictions.txt"
        File output_predictions_ins = "${sample_name}.INS.output.predictions.hg38_multianno.txt"

        File input_predictions_del = "${sample_name}.DEL.input.predictions.txt"
        File output_predictions_del = "${sample_name}.DEL.output.predictions.hg38_multianno.txt"
    }

    runtime {
        docker: "junhuili/terra_perl_anno:latest"
        disks: "local-disk " + 100 + " SSD"
        memory: "32 GB"
        cpu: 4
    }
}

task ExtractSubvcf {
    input {
        File vcf_gz
        File predictions_snv
        File predictions_ins
        File predictions_del
    }
    String sample_name_snv = sub(basename(predictions_snv),"\\.input\\.predictions\\.txt","")
    String sample_name_ins = sub(basename(predictions_ins),"\\.input\\.predictions\\.txt","")
    String sample_name_del = sub(basename(predictions_del),"\\.input\\.predictions\\.txt","")
    command <<<
        cat ~{predictions_snv} | awk 'BEGIN{OFS="\t"}{print $1,$2-1,$2}' | sort -k1,1 -k2,2n > ~{sample_name_snv}.subvcf.bed
        if [[ "~{vcf_gz}" == *.gz ]]; then
            tabix ~{vcf_gz} -R ~{sample_name_snv}.subvcf.bed > ~{sample_name_snv}.sub.vcf.tmp
            cat <(zcat ~{vcf_gz} | grep "^#") <(cat ~{sample_name_snv}.sub.vcf.tmp) > ~{sample_name_snv}.sub.vcf
        else
            bedtools intersect -a ~{vcf_gz} -b ~{sample_name_snv}.subvcf.bed > ~{sample_name_snv}.sub.vcf.tmp
            cat <(cat ~{vcf_gz} | grep "^#") <(cat ~{sample_name_snv}.sub.vcf.tmp) > ~{sample_name_snv}.sub.vcf
        fi
        cut -f 1,2,4,5,10 ~{sample_name_snv}.sub.vcf.tmp | sed "s/:/\t/g" | cut -f 1-8 | sed 's/,/\t/g' | awk 'BEGIN{print "Chr\tpos\tREF\tALT\tGenotype\tDPT_REF\tDPT_ALT\tAF\tDPT"}1' > ~{sample_name_snv}.Geno.DP.AF.txt

        cat ~{predictions_ins} | awk 'BEGIN{OFS="\t"}{print $1,$2-1,$2}' | sort -k1,1 -k2,2n > ~{sample_name_ins}.subvcf.bed
        if [[ "~{vcf_gz}" == *.gz ]]; then
            tabix ~{vcf_gz} -R ~{sample_name_ins}.subvcf.bed > ~{sample_name_ins}.sub.vcf.tmp
            cat <(zcat ~{vcf_gz} | grep "^#") <(cat ~{sample_name_ins}.sub.vcf.tmp) > ~{sample_name_ins}.sub.vcf
        else
            bedtools intersect -a ~{vcf_gz} -b ~{sample_name_ins}.subvcf.bed > ~{sample_name_ins}.sub.vcf.tmp
            cat <(cat ~{vcf_gz} | grep "^#") <(cat ~{sample_name_ins}.sub.vcf.tmp) > ~{sample_name_ins}.sub.vcf
        fi
        cut -f 1,2,4,5,10 ~{sample_name_ins}.sub.vcf.tmp | sed "s/:/\t/g" | cut -f 1-8 | sed 's/,/\t/g' | awk 'BEGIN{print "Chr\tpos\tREF\tALT\tGenotype\tDPT_REF\tDPT_ALT\tAF\tDPT"}1' > ~{sample_name_ins}.Geno.DP.AF.txt

        cat ~{predictions_del} | awk 'BEGIN{OFS="\t"}{print $1,$2-1,$2}' | sort -k1,1 -k2,2n > ~{sample_name_del}.subvcf.bed
        if [[ "~{vcf_gz}" == *.gz ]]; then
            tabix ~{vcf_gz} -R ~{sample_name_del}.subvcf.bed > ~{sample_name_del}.sub.vcf.tmp
            cat <(zcat ~{vcf_gz} | grep "^#") <(cat ~{sample_name_del}.sub.vcf.tmp) > ~{sample_name_del}.sub.vcf
        else
            bedtools intersect -a ~{vcf_gz} -b ~{sample_name_del}.subvcf.bed > ~{sample_name_del}.sub.vcf.tmp
            cat <(cat ~{vcf_gz} | grep "^#") <(cat ~{sample_name_del}.sub.vcf.tmp) > ~{sample_name_del}.sub.vcf
        fi
        cut -f 1,2,4,5,10 ~{sample_name_del}.sub.vcf.tmp | sed "s/:/\t/g" | cut -f 1-8 | sed 's/,/\t/g' | awk 'BEGIN{print "Chr\tpos\tREF\tALT\tGenotype\tDPT_REF\tDPT_ALT\tAF\tDPT"}1' > ~{sample_name_del}.Geno.DP.AF.txt
    >>>

    output {
        File sub_vcf_snv = "~{sample_name_snv}.sub.vcf"
        File subvcf_bed_snv = "~{sample_name_snv}.subvcf.bed"
        File geno_dp_af_snv = "~{sample_name_snv}.Geno.DP.AF.txt"
        File sub_vcf_ins = "~{sample_name_ins}.sub.vcf"
        File subvcf_bed_ins = "~{sample_name_ins}.subvcf.bed"
        File geno_dp_af_ins = "~{sample_name_ins}.Geno.DP.AF.txt"
        File sub_vcf_del = "~{sample_name_del}.sub.vcf"
        File subvcf_bed_del = "~{sample_name_del}.subvcf.bed"
        File geno_dp_af_del = "~{sample_name_del}.Geno.DP.AF.txt"
    }

    runtime {
        docker: "quay.io/biocontainers/bedtools:2.30.0--h7d7f7ad_2"
    }
}

task gnomAD_filter_vcf_anno {
    input {
        File vcf
        File anno
        File gnomad_2_1_1_0_001
        File gnomad_2_1_1_0
    }
    String sample_name = sub(basename(vcf),"\\.vcf","")
    command <<<
        awk 'NR==FNR{c[$1,$2,$4,$5]++;next} !(c[$1,$2,$4,$5]) {print $0}' ~{gnomad_2_1_1_0_001} ~{vcf} > ~{sample_name}.MFv4.vcf
        awk 'NR==FNR{c[$1,$2,$4,$5]++;next} !(c[$1,$2,$4,$5]) {print $0}' ~{gnomad_2_1_1_0} ~{vcf} > ~{sample_name}.MFv3.vcf
        cat <(head -n 1 ~{anno}) <(awk 'NR==FNR{{c[$1,$2,$4,$5]++;next}} c[$1,$2,$4,$5]>0{{print $0}}' ~{sample_name}.MFv4.vcf ~{anno}) > ~{sample_name}.MFv4.txt
        cat <(head -n 1 ~{anno}) <(awk 'NR==FNR{{c[$1,$2,$4,$5]++;next}} c[$1,$2,$4,$5]>0{{print $0}}' ~{sample_name}.MFv3.vcf ~{anno}) > ~{sample_name}.MFv3.txt
        awk 'BEGIN{OFS="\t"}NR==FNR{c[$1,$2,$4,$5]=$0} NR!=FNR{if (c[$2,$3,$5,$6]) {print "tier2\t"$0} else {print "tier3\t"$0}}' ~{sample_name}.MFv4.txt \
        <(awk 'BEGIN{OFS="\t"}NR==FNR{c[$1,$2,$4,$5]=$0} NR!=FNR{if (c[$1,$2,$4,$5]) {print "tier1\t"$0} else {print "tier3\t"$0}}' ~{sample_name}.MFv3.txt \
        ~{anno}) | sed 's/tier2\ttier1/tier1/g' | sed 's/tier2\ttier3/tier2/g' | sed 's/tier3\ttier3/tier3/g' |sed 's/tier3\ttier1/tier1/g' | sed 's/tier1\tChr/Tier\tChr/' \
        > ~{sample_name}.tier.txt
    >>>

    output {
        File vcf4 = "~{sample_name}.MFv4.vcf"
        File vcf3 = "~{sample_name}.MFv3.vcf"
        File anno4 = "~{sample_name}.MFv4.txt"
        File anno3 = "~{sample_name}.MFv3.txt"
        File tier = "~{sample_name}.tier.txt"
    }

    runtime {
        docker: "ubuntu:latest"
        cpu: 4
        memory: "8 GB"
        disks: "local-disk 100 SSD"
    }
}

task DepthOfCoverage {
    input {
        File intervals
        File ref_fasta
        File ref_fai
        File ref_dict
        File tumor_bam
        File tumor_bam_index
        # runtime
        String gatk_docker
        Int? mem
        Int? preemptible
        Int? max_retries
        Int? disk_space
        Int? cpu
        Boolean use_ssd = false
        File? gatk_override
    }
    String name = sub(basename(tumor_bam),"\\.bam","")
    Int machine_mem = if defined(mem) then mem * 1000 else 3500
    Int command_mem = machine_mem - 500

    command {
        set -e
        export GATK_LOCAL_JAR=~{default="/root/gatk.jar" gatk_override}
        gatk --java-options "-Xmx~{command_mem}m" DepthOfCoverage \
        -R ~{ref_fasta} \
        -I ~{tumor_bam} \
        -L ~{intervals} \
        -O ~{name}
        set +e
    }
    output {
        File depth_of_cov_statistics = "~{name}.sample_statistics"
    }
    runtime {
        docker: gatk_docker
        bootDiskSizeGb: 12
        memory: machine_mem + " MB"
        disks: "local-disk " + select_first([disk_space, 100]) + if use_ssd then " SSD" else " HDD"
        preemptible: select_first([preemptible, 10])
        maxRetries: select_first([max_retries, 0])
        cpu: select_first([cpu, 1])
    }
}

task MergeCovStatistics {
        input {
            Array[File] statistics
        }
        
        command <<<
            file_list=$(printf "%s " ~{sep=' ' statistics})

            awk -F"," 'NR==1 && FNR==1 {header=$0; next} {for(i=2; i<=NF; i++) sum[i] += $i} 
            END {
                print header
                printf "%s", $1
                for(i=2; i<=NF; i++) printf ",%d", sum[i]
                print ""
            }' $file_list > output.sample_statistics.txt
        >>>
        output {
            File stat_all = "output.sample_statistics.txt"
        }
        runtime {
            docker: "junhuili/terra_init:latest"
            cpu: 2
            memory: "8 GB"
            disks: "local-disk 5 HDD"
        }
}
